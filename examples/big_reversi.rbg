#game = "Reversi"

#halfWithoutOne = empty,empty,empty,empty,empty,empty,empty
#row = halfWithoutOne, empty, empty, halfWithoutOne

#board = [row]
         [row]
         [row]
         [halfWithoutOne, wpiece, bpiece, halfWithoutOne]
         [halfWithoutOne, bpiece, wpiece, halfWithoutOne]
         [row]
         [row]
         [row]

#players = wplayer, bplayer

#pieces = empty, wpiece, bpiece
#variables=

#anySquare =
    (
        ( 1, 0)
      + ( 0, 1)
      + (-1, 0)
      + ( 0,-1)
    )^*


#swapdirectionpossible (pla; opp; direction; oppositedirection) =
    (
        direction
        (opp~piece)
        (direction (opp~piece))^*
        direction
        (pla~piece)
    )

#swapdirection (pla; opp; direction; oppositedirection) =
    (
        (
            direction
            (opp~piece)
            [pla~piece]
        )^*
        direction
        (pla~piece)
        oppositedirection
        ((pla~piece) oppositedirection)^*
        (empty)
        +
        (? not (!  swapdirectionpossible(pla; opp; direction; oppositedirection)))
    )

#swapdirections (pla;opp) =
    (
        swapdirection(pla; opp; (-1,0); (1,0))
        swapdirection(pla;opp;(1,0);(-1,0))
        swapdirection(pla;opp;(0,1);(0,-1))
        swapdirection(pla;opp;(0,-1);(0,1))
        swapdirection(pla;opp;(-1,-1);(1,1))
        swapdirection(pla;opp;(1,1);(-1,-1))
        swapdirection(pla;opp;(1,-1);(-1,1))
        swapdirection(pla;opp;(-1,1);(1,-1))
    )

#movepossible (pla; opp) =
    (
        swapdirectionpossible(pla; opp; (-1,0); (1,0))
        +
        swapdirectionpossible(pla;opp;(1,0);(-1,0))
        +
        swapdirectionpossible(pla;opp;(0,1);(0,-1))
        +
        swapdirectionpossible(pla;opp;(0,-1);(0,1))
        +
        swapdirectionpossible(pla;opp;(-1,-1);(1,1))
        +
        swapdirectionpossible(pla;opp;(1,1);(-1,-1))
        +
        swapdirectionpossible(pla;opp;(1,-1);(-1,1))
        +
        swapdirectionpossible(pla;opp;(-1,1);(1,-1))
    )

#move (pla; opp) =
    (
        anySquare
        (empty)
        ( ? (! movepossible(pla;opp)))
        swapdirections(pla; opp)
        [pla~piece]
        +
        (? not (! anySquare (empty) movepossible(pla;opp)))
    )

#setpoints =
    (
            (? $wpiece > $bpiece) [$bplayer = 0][$wplayer = 100]
            +
            (? $wpiece < $bpiece) [$bplayer = 100][$wplayer = 0]
            +
            (? $wpiece == $bpiece) [$bplayer = 50][$wplayer = 50]
    )


#rules = 
    (
        move(w;b)
        setpoints
        ->bplayer

        move(b;w)
        setpoints
        ->wplayer

        (
        (? (! anySquare (empty) movepossible(b;w)))
        +
        (? (! anySquare (empty) movepossible(w;b)))
        )
    )^*

#finalizer = ()

