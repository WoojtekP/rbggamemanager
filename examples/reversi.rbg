#game = "Reversi"

#row = [empty,empty,empty,empty,empty,empty,empty,empty]

#board = row
         row
         row
         [empty,empty,empty,wpiece,bpiece,empty,empty,empty]
         [empty,empty,empty,bpiece,wpiece,empty,empty,empty]
         row
         row
         row

#players = wplayer, bplayer

#bound = 65

#pieces = empty, wpiece, bpiece
#variables =

#anySquare = (((1,0)^*+(-1,0)^*)((0,1)^*+(0,-1)^*))

#swapdirectionpossible (pla; opp; direction; oppositedirection) =
    (
        direction
        (opp~piece)
        (direction (opp~piece))^*
        direction
        (pla~piece)
    )

#swapdirection (pla; opp; direction; oppositedirection) =
    (
        (
            direction
            (opp~piece)
            [pla~piece]
        )^*
        direction
        (pla~piece)
        oppositedirection
        ((pla~piece) oppositedirection)^*
        (empty)
        +
        (? not (! swapdirectionpossible(pla; opp; direction; oppositedirection)))
    )

#swapdirections (pla;opp) =
    (
        swapdirection(pla; opp; (-1,0); (1,0))
        swapdirection(pla;opp;(1,0);(-1,0))
        swapdirection(pla;opp;(0,1);(0,-1))
        swapdirection(pla;opp;(0,-1);(0,1))
        swapdirection(pla;opp;(-1,-1);(1,1))
        swapdirection(pla;opp;(1,1);(-1,-1))
        swapdirection(pla;opp;(1,-1);(-1,1))
        swapdirection(pla;opp;(-1,1);(1,-1))
    )

#movepossible (pla; opp) =
    (
        swapdirectionpossible(pla; opp; (-1,0); (1,0))
        +
        swapdirectionpossible(pla;opp;(1,0);(-1,0))
        +
        swapdirectionpossible(pla;opp;(0,1);(0,-1))
        +
        swapdirectionpossible(pla;opp;(0,-1);(0,1))
        +
        swapdirectionpossible(pla;opp;(-1,-1);(1,1))
        +
        swapdirectionpossible(pla;opp;(1,1);(-1,-1))
        +
        swapdirectionpossible(pla;opp;(1,-1);(-1,1))
        +
        swapdirectionpossible(pla;opp;(-1,1);(1,-1))
    )


#setpoints =
    (
            (? $wpiece > $bpiece) [$bplayer = 0][$wplayer = 2]
            +
            (? $wpiece < $bpiece) [$bplayer = 2][$wplayer = 0]
            +
            (? $wpiece == $bpiece) [$bplayer = 1][$wplayer = 1]
    )

#move (pla; opp) =
    (
        anySquare
        (empty)
        ( ? (! movepossible(pla;opp)))
        ->
        swapdirections(pla; opp)
        [pla~piece]
        +
        (? not (! anySquare (empty) movepossible(pla;opp)))
        (? (!anySquare (empty) movepossible(opp;pla)))
    )



#rules =
    ->wplayer
    (
        move(w;b)
        setpoints
        ->bplayer

        move(b;w)
        setpoints
        ->wplayer
    )^*

